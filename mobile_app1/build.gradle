buildscript {
    ext.kotlin_version = providers.systemProperty('kotlinVersion').forUseAtConfigurationTime().orNull ?: '1.4.30'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${providers.systemProperty("agpVersion").forUseAtConfigurationTime().orNull ?: '4.2.0-beta04'}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        if ((providers.systemProperty('okbuck.wrapper').forUseAtConfigurationTime().orNull ?: 'false').toBoolean()) {
            classpath "com.uber:okbuck:0.52.3"
        }
    }
}
plugins {

}
allprojects {
    repositories {
        google()
        jcenter()
    }
    // Disable unused build variants
    plugins.withType(com.android.build.gradle.BasePlugin) {
        androidComponents {
          beforeVariants(selector().withBuildType("release"), { enabled = false })
          beforeUnitTests(selector().all(), { enabled = false })
          beforeAndroidTests(selector().all(), { enabled = false })
        }
    }
}
task clean(type: Delete) {
    delete rootProject.buildDir
}
buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    link 'GitHub', 'https://github.com/uber-common/android-build-eval'
}
// Only apply the okbuck Gradle plugin when generating the wrapper and BUCK files.
if ((providers.systemProperty('okbuck.wrapper').forUseAtConfigurationTime().orNull ?: 'false').toBoolean()) {
    apply plugin: 'com.uber.okbuck'

    okbuck {
        buckBinary = "com.github.raviagarwal7:buck:8369cecf8b4a8d628f7852f0030587944f01bc19@pex"
    }
}